plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
}

group 'com.lifeinide.jsonql'
description 'ElasticSearch JSON query builder using GSON.'

/*
  VERSION UPGRADE HOWTO:
  1. Increment version here
  2. Tag with the same version tag and push
  3. Do `gradle publish`
 */
version '1.0.3'

sourceCompatibility = 1.8

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

def token = project.findProperty("token")
if (!token) {
    try {
        token = new File(System.getProperty("user.home"), ".github-token").text
        logger.info("Using token from ~/.github-token.")
    } catch (e) {
        logger.warn("Please put personal github token into ~/.github-token file or pass it with -Ptoken=??? parameter.")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml { // https://discuss.gradle.org/t/maven-publish-plugin-generated-pom-making-dependency-scope-runtime/7494/10
                asNode().dependencies.'*'.each { it.scope*.value = 'compile'}
            }
            from components.java
            artifact sourcesJar
//            artifact javadocJar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/json-ql/jsonql-core")
            credentials {
                username = project.findProperty("user") ?: "l0co"
                password = token
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/json-ql/jsonql-core")
        credentials {
            username = project.findProperty("user") ?: "l0co"
            password = token
        }
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: property('vSlf4j')
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: property('vJsr305')
    compile group: 'com.google.code.gson', name: 'gson', version: property('vGson')

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: property('vJunit')
    testCompile group: 'commons-io', name: 'commons-io', version: property('vCommonsIo')
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: property('vJunit')
    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: property('vLogback')
}

test {
    useJUnitPlatform()
}

def installer = install.repositories.mavenInstaller
